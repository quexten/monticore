/* (c) https://github.com/MontiCore/monticore */

/** This is a very much simplified Java excerpt that 
    exhibits some of typical Java symbols and scopes.
    It is used for demonstration only.
*/

grammar BasicJava extends de.monticore.MCBasics {

CompilationUnit =  TypeDeclaration* EOF ;

// Type declarations
interface TypeDeclaration;

// Usage of a type
interface Type;

//Type used in return statements
interface ReturnType;

// pseudo interface implementations to produce non-component grammar
DummyType implements Type  =  b:"boolean" | s:"String" ;
DummyReturnType implements ReturnType  = b:"boolean" | v:"void" ;

symbol scope ClassDeclaration implements TypeDeclaration
    =   "class" Name
        ("extends" superClass:Type)?
        ("implements" implementedInterfaces:(Type || ",")+)?
        ClassBody
    ;

symbol scope InterfaceDeclaration implements TypeDeclaration
    =   "interface" Name
        ("extends" extendedInterfaces:(Type || ",")+)?
        ClassBody
    ;

// Interface-Symbols are special forms of class-Symbols
symbolrule InterfaceDeclaration extends ASTClassDeclaration;

ClassBody
    =   "{" (Method|Field)* "}"
    ;

symbol scope Method
    =   ReturnType Name FormalParameters
        (JavaBlock | ";")
    ;

symbol ConstructorDeclaration
    =   Name FormalParameters
    ;

symbol Field
    =   Type Name (dim:"[" "]")* ("=" Expression)? ";"
    ;

symbol VariableDeclarator
    =   Name (dim:"[" "]")* ("=" Expression)?
    ;

FormalParameters
    =   "(" (FormalParameter || ",")* ")"
    ;

symbol FormalParameter
    =   Type Name (dim:"[" "]")*
    ;

// STATEMENTS / BLOCKS

interface Statement ;

LocalVariableDeclarationStatement implements Statement
    =    Type (VariableDeclarator || ",")+ ";"
    ;

JavaBlock implements Statement
    =   "{" Statement* "}"
    ;

ExpressionStatement implements Statement
    =   Expression ";"
    ;

// EXPRESSIONS

Expression
    =   
    |   Expression "." Name@VariableDeclarator
    |   "(" Expression ")"
    |   Name@VariableDeclarator
    ;

}
